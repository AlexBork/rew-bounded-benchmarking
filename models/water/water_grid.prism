// Simple example of a robot moving on a grid with a water obstacle
// The robot can move north, south, east or west
// The water tiles are either deep or shallow, causing different energy costs
// Additionally, with some probability, the water level is low, causing crossing to be easier
// Rewards/Costs are designed such that the policy minimising expected energy cost is not viable if the energy bound is <6
// Sketch
// Goal ---- ----
// Deep Shal ----
// Home ---- ----

pomdp

// Constants that should technically be changeable (changeability of N might depend on map description)
const int N = 3;
const double slippery = 0;
const double lowWaterLevelProb = 0.5;

// General stuff
const int xMIN = 0;
const int xMAX = N-1;
const int yMIN = 0;
const int yMAX = N-1;

formula deepWater = (x=0 & y=1);
formula shallowWater = (x=1 & y=1);

formula onWater = deepWater | shallowWater;

formula waterNorth = (y=0 & x=0) | (y=0 & x=1) ;
formula waterSouth = (x=1 & y=2) | (x=0 & y=2);
formula waterEast = (y=1 & x=0);
formula waterWest = (y=1 & x=1) | (y=1 & x=2);

// Location of goal
formula goal = (x = 0) & (y = yMAX);

// Observables
observable "done" = goal;
observable "initalised" = initialised;

module robot
  x : [xMIN..xMAX] init xMIN; // x coordinate, min is west, max is east
  y : [yMIN..yMAX] init yMIN; // y coordinate, min is south, max is north
  lowWaterLevel : bool init false;
  initialised : bool init false;

  [prep] !initialised -> lowWaterLevelProb: (lowWaterLevel'=true) & (initialised'=true) + (1-lowWaterLevelProb): (lowWaterLevel'=false) & (initialised'=true);

  [north] initialised -> slippery: (y'=y) + (1-slippery): (y'=min(y+1, yMAX));
  [east] initialised -> slippery: (x'=x) + (1-slippery): (x'=min(x+1, xMAX));
  [west] initialised -> slippery: (x'=x) + (1-slippery): (x'=max(x-1, xMIN));
  [south] initialised -> slippery: (y'=y) + (1-slippery): (y'=max(y-1, yMIN));
endmodule


// Rewards

rewards "steps"
  [north] true : 1;
  [east] true : 1;
  [south] true : 1;
  [west] true : 1;
endrewards

rewards "energy"
  [north] (deepWater & lowWaterLevel) | (shallowWater & !lowWaterLevel)  : 6;
  [east]  (deepWater & lowWaterLevel) | (shallowWater & !lowWaterLevel)  : 6;
  [south]  (deepWater & lowWaterLevel) | (shallowWater & !lowWaterLevel)  : 6;
  [west]  (deepWater & lowWaterLevel) | (shallowWater & !lowWaterLevel)  : 6;

  [north] (shallowWater & lowWaterLevel)  : 2;
  [east]  (shallowWater & lowWaterLevel)  : 2;
  [south]  (shallowWater & lowWaterLevel)  : 2;
  [west]  (shallowWater & lowWaterLevel)  : 2;

  [north] (deepWater & !lowWaterLevel)  : 10;
  [east]  (deepWater & !lowWaterLevel)  : 10;
  [south]  (deepWater & !lowWaterLevel)  : 10;
  [west]  (deepWater & !lowWaterLevel)  : 10;

  [north] !deepWater & !shallowWater : 1;
  [east]  !deepWater & !shallowWater : 1;
  [south] !deepWater & !shallowWater : 1;
  [west]  !deepWater & !shallowWater : 1;
endrewards


// Labels
label "goal" = goal;